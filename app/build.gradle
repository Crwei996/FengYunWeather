plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
}

apply plugin: per.wsj.plugin.PublishPlugin
//apply plugin: 'com.google.devtools.ksp'

publishConfig {
    publishUrl configProperties.get("publish_url")
    ossEndpoint configProperties.get("oss_endpoint")
    ossAkId configProperties.get("oss_ak_id")
    ossAkSecret configProperties.get("oss_ak_secret")
    apkPath "${rootDir}/app/release/app-release.apk"
    versionDesc "1,增加插件换肤功能\n2,提升性能，优化用户体验。"
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    defaultConfig {
        applicationId "me.wsj.fengyun"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 14
        versionName "1.1.4"

        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //指定room.schemaLocation生成的文件路径
        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        // 过滤语言包
        resConfigs('zh-rCN', 'en')
    }

    signingConfigs {
        debug {
            if (configProperties != null && configProperties.get("filename") != null){
                storeFile file(configProperties.get("filename"))
                storePassword configProperties.get("storePassword")
                keyAlias configProperties.get("keyAlias")
                keyPassword configProperties.get("keyPassword")
            }
        }
    }

    buildTypes {
        release {
            zipAlignEnabled true
            shrinkResources true
            minifyEnabled true
            manifestPlaceholders = [amap_app_key: configProperties.get("gao_de_app_key")]
            buildConfigField('String', 'HeFengKey', configProperties.get("he_feng_app_key"))
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.debug
            // todo 请在local.properties中配置天气及定位的api key
            manifestPlaceholders = [amap_app_key: configProperties.get("gao_de_app_key")]
            buildConfigField('String', 'HeFengKey', configProperties.get("he_feng_app_key"))
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation project(path: ':lib')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation project(path: ':plugin-lib')
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "com.google.android.material:material:$material_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_service"

    implementation files('libs/AMap_Location_V5.3.1_20210331.jar')

    implementation 'com.google.dagger:hilt-android:2.36'
    kapt 'com.google.dagger:hilt-compiler:2.36'

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
//    ksp "androidx.room:room-compiler:$room_version"

//    debugImplementation  'com.github.wsj1024:CrashCanary:1.0.4'
//
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'
}